apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'io.fabric'

def AAVersion = '4.3.0'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def getMyApiKey(String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(rootProject.file("apikey")))
    return "\"" + properties.getProperty(property) + "\""
}

android {
    signingConfigs {
        debug {
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storeFile file(keystoreProperties['debugStoreFile'])
            storePassword keystoreProperties['debugStorePassword']
        }
        release {
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
            storeFile file(keystoreProperties['releaseStoreFile'])
            storePassword keystoreProperties['releaseStorePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "tw.tonyyang.englishwords"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField "String", "DROPBOX_API_KEY", getMyApiKey("dropBoxApiKey")
            manifestPlaceholders = [FACEBOOK_APP_ID         : getMyApiKey("facebookAppId"),
                                    FB_LOGIN_PROTOCOL_SCHEME: getMyApiKey("fbLoginProtocolScheme")]
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DROPBOX_API_KEY", getMyApiKey("dropBoxApiKey")
            manifestPlaceholders = [FACEBOOK_APP_ID         : getMyApiKey("facebookAppId"),
                                    FB_LOGIN_PROTOCOL_SCHEME: getMyApiKey("fbLoginProtocolScheme")]
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0]?.processResources?.manifestFile
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:25.3.1'
    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"
    apt "org.androidannotations:ormlite:$AAVersion"
    compile "org.androidannotations:ormlite-api:$AAVersion"
    compile 'org.slf4j:slf4j-api:1.7.6'
    compile "com.j256.ormlite:ormlite-core:4.48"
    compile "com.j256.ormlite:ormlite-android:4.48"
    compile project(':dropboxChooserSDK')
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.hedgehog.ratingbar:app:1.1.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    compile 'com.facebook.android:facebook-android-sdk:4.23.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.facebook.stetho:stetho:1.5.0'
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
    mavenLocal()
}